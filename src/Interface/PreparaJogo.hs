module Interface.PreparaJogo where
import System.Console.ANSI
import System.IO
import Jogo.Logica
import Jogo.Jogo (jogo, instrucoes)

--------------------------------------------------------------------------------
-- | Configura o terminal para a execu√ß√£o do jogo.
--
-- A√ß√µes realizadas:
--   1. Esconde o cursor;
--   2. Desativa o 'echo';
--   3. Desativa o buffering;
--   4. Limpa a tela inicial.
--------------------------------------------------------------------------------
setUpTerminal :: IO()
setUpTerminal = do
    hideCursor
    hSetEcho stdin False
    hSetBuffering stdin NoBuffering
    clearScreen

--------------------------------------------------------------------------------
-- | Exibe o menu principal do jogo.
--
-- Par√¢metros:
--   @(l, c)@ ‚Äì posi√ß√£o central onde o menu ser√° desenhado.
--
-- O menu apresenta as op√ß√µes:
--   * F√ÅCIL
--   * M√âDIO
--   * DIF√çCIL
--   * SAIR
--
-- A seta "->" come√ßa posicionada sobre a primeira op√ß√£o.
--------------------------------------------------------------------------------
menu :: (Int,Int) -> IO ()
menu (l,c)= do
    setCursorPosition l (c-10)
    putStr "->"
    hFlush stdout
    setCursorPosition (l-4) (c-6)
    putStrLn "CAMPüí£  MINADüí•"
    setCursorPosition (l-2) (c-37)
    putStrLn "Use W/S ou Setas e ENTER para escolher a dificuldade"
    
    -- Op√ß√µes
    setCursorPosition l (c-2)
    putStrLn "F√ÅCIL"
    setCursorPosition (l+1) (c-2)
    putStrLn "M√âDIO"
    setCursorPosition (l+2) (c-3)
    putStrLn "DIF√çCIL"
    setCursorPosition (l+3) (c-2)
    putStrLn "SAIR"

--------------------------------------------------------------------------------
-- | Gerencia o fluxo de in√≠cio de jogo.
--
-- Par√¢metros:
--   @(l, c)@ ‚Äì posi√ß√£o central da tela.
--
-- A fun√ß√£o:
--   1. Chama 'dificuldade' para obter o n√≠vel (9, 16, 20 ou 0).
--   2. Limpa a tela.
--   3. Desenha o tabuleiro adequado ao n√≠vel.
--   4. Desenha instru√ß√µes do jogo.
--   5. Inicia a fun√ß√£o principal 'jogo'.
--
-- Retorno:
--   Se o jogador escolher SAIR, retorna imediatamente.
--------------------------------------------------------------------------------
comecarJogo :: (Int, Int) -> IO ()
comecarJogo (l,c) = do
    nivel <- dificuldade (l, c - 10) l
    clearScreen
    if nivel == 0
        then return ()
        else do
            let inicioL = l - (nivel)
            let inicioC = c - (nivel*2)
            let limite  = (nivel * 2) 
            
            tabuleiro (inicioL, inicioC) nivel limite
            
            instrucoes (l + nivel + 2)
            
            let limiteSuperior = inicioL + 1
            let limiteInferior = (nivel - 1) + l
            let limiteEsquerdo = inicioC + 2
            let limiteDireito  = (nivel * 2) + c - 2
            
            jogo (limiteSuperior, limiteEsquerdo) ((limiteSuperior, limiteEsquerdo), (limiteInferior, limiteDireito)) []

--------------------------------------------------------------------------------
-- | Fun√ß√£o recursiva que desenha o grid do tabuleiro/ o tabuleiro ASCII do jogo.
--
-- Alterna entre desenhar linhas de separa√ß√£o (+---+) e linhas de c√©lulas (|   |).
--
-- Par√¢metros:
--   * @(linha, coluna)@ ‚Äì posi√ß√£o de in√≠cio do tabuleiro
--   * @n@ ‚Äì largura l√≥gica (relacionada ao tamanho real em colunas)
--   * @limite@ ‚Äì n√∫mero de linhas restantes para desenhar
--
-- O tabuleiro √© constru√≠do recursivamente, alternando entre linhas:
--   * √≠mpares -> '|   |   | ... |'
--   * pares   -> '+---+---+ ... +'
--
-- Quando limite chega a 0, a √∫ltima linha horizontal √© desenhada.
--------------------------------------------------------------------------------
tabuleiro :: (Int, Int) -> Int -> Int -> IO ()
tabuleiro (linha,coluna) n limite = do
    setCursorPosition linha coluna
    if limite == 0 then
        putStrLn (take ((n*4)+1) (cycle "+---"))
    else do
        if even limite
            then putStrLn (take ((n*4)+1) (cycle "+---"))
            else putStrLn (take ((n*4)+1) (cycle "|   "))

        tabuleiro (linha + 1, coluna) n (limite - 1)

--------------------------------------------------------------------------------
-- | L√≥gica de sele√ß√£o do Menu (Input).
--
-- Permite navegar entre as op√ß√µes usando Setas ou W/S.
-- Par√¢metros:
--   * @(linha, coluna)@ ‚Äì posi√ß√£o atual do cursor "->"
--   * @limite@ ‚Äì linha onde come√ßa a primeira op√ß√£o do menu
--
-- Comandos suportados:
--   * @\"\ESC[A\"@ -> seta para cima
--   * @\"\ESC[B\"@ -> seta para baixo
--   * @\"\n\"@ -> Enter
--
-- A seta move pelas 4 op√ß√µes usando aritm√©tica modular:
--
-- > (mod (index ¬± 1) 4) + limite
--
-- Retorno (Int):
--   * 9  -> F√°cil
--   * 16 -> M√©dio
--   * 20 -> Dif√≠cil
--   * 0  -> Sair
--------------------------------------------------------------------------------
dificuldade :: (Int,Int) -> Int-> IO Int
dificuldade (linha,coluna) limite = do
    comando <- getKey
    setCursorPosition linha coluna
    putStr "  "
    hFlush stdout
    
    let index = linha - limite
    let novaLinha
            -- Cima (Seta UP ou W)
            | comando == "\ESC[A" || comando == "w" || comando == "W" = (mod (index-1) 4) + limite
            -- Baixo (Seta DOWN ou S)
            | comando == "\ESC[B" || comando == "s" || comando == "S" = (mod (index+1) 4) + limite
            | otherwise = linha
    setCursorPosition novaLinha coluna
    putStr "->"
    hFlush stdout

    -- Verifica sele√ß√£o
    if comando == "\n" -- Enter
        then 
            if linha == limite then return 9        -- F√°cil
            else if linha == (limite+1) then return 16 -- M√©dio
            else if linha == (limite+2) then return 20 -- Dif√≠cil
            else return 0                              -- Sair
        else dificuldade (novaLinha, coluna) limite